extends Node

# The URL we will connect to.
@export var websocket_url = "127.0.0.1:1234"
@onready var line_edit = $LineEdit
@onready var debug = $debug

# Our WebSocketClient instance.
var socket := WebSocketPeer.new()

func _ready():
	# Initiate connection to the given URL.
	var err = socket.connect_to_url(websocket_url)
	if err != OK:
		print("Unable to connect")
		set_process(false)
	else:
		# Wait for the socket to connect.
		print("Connecting...")
		await wait_for_connection()
		# Send data.
		var send_err = socket.send_text("Godot is connected")
		if send_err != OK:
			print("Failed to send data: ", send_err)

# Helper function to wait until the connection is open
func wait_for_connection() -> void:
	while socket.get_ready_state() != WebSocketPeer.STATE_OPEN:
		socket.poll() # Keep polling for updates
		await get_tree().create_timer(0.1).timeout
	print("Connection established!")

func _process(_delta):
	# Call this in _process or _physics_process. Data transfer and state updates
	# will only happen when calling this function.
	socket.poll()

	# get_ready_state() tells you what state the socket is in.
	var state = socket.get_ready_state()

	# WebSocketPeer.STATE_OPEN means the socket is connected and ready
	# to send and receive data.
	if state == WebSocketPeer.STATE_OPEN:
		while socket.get_available_packet_count():
			print("Got data from server: ", socket.get_packet().get_string_from_utf8())

	# WebSocketPeer.STATE_CLOSING means the socket is closing.
	# It is important to keep polling for a clean close.
	elif state == WebSocketPeer.STATE_CLOSING:
		pass

	# WebSocketPeer.STATE_CLOSED means the connection has fully closed.
	# It is now safe to stop polling.
	elif state == WebSocketPeer.STATE_CLOSED:
		# The code will be -1 if the disconnection was not properly notified by the remote peer.
		var code = socket.get_close_code()
		print("WebSocket closed with code: %d. Clean: %s" % [code, code != -1])
		set_process(false) # Stop processing.


func _on_button_pressed():
	var message = line_edit.text
	if message == "":
		print("Cannot send an empty message.")
		return
	
	if socket.get_ready_state() == WebSocketPeer.STATE_OPEN:
		var send_err = socket.send_text(message)
		if send_err == OK:
			print("Sent: ", message)
			debug.text = ""  # Clear the input field after sending
		else:
			print("Failed to send message: ", send_err)
	else:
		print("Cannot send message: Socket not open.")
